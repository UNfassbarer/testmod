plugins {
	id 'fabric-loom' version '1.6.5'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	maven { url "https://maven.shedaniel.me" }
	maven {
		name = 'TerraformersMC'
		url = 'https://maven.terraformersmc.com/'
	}
	maven { url = 'https://maven.minecraftforge.net/' }
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// REI API for Fabric
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:14.0.687"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:14.0.687"
	// REI Runtime for Fabric
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:14.0.687"

	modApi "dev.architectury:architectury-fabric:11.0.8"

	modImplementation 'com.github.glitchfiend:TerraBlender-fabric:1.20.4-3.3.0.12'

	modApi("me.shedaniel.cloth:cloth-config-fabric:13.0.114") {
		exclude(group: "net.fabricmc.fabric-api")
	}

}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
